-- Resta de naturales
def sub = \n m. m pred n

-- x <= y
def le = \x y. is0 (sub x y)

-- Potencia de dos
def pow2 = \x. mult x x

-- Busca el minimo n tal que x < (n+1)^2
def sqrti = Y (\f x n. (le (pow2 (suc n)) x) (f x (suc n)) (n))

-- Raiz entera
def sqrt = \x. sqrti x zero 

-- Busca el minimo n tal que x < (n+1)*y
def divi = Y (\f x y n. (le (mult (suc n) y) x) (f x y (suc n)) (n))

-- Division entera
def div = \x y. divi x y zero

-- Resto
def mod = Y (\f x y. (is0 (sub y x)) (f (sub x y) y) (x) )

-- Busca m que divida n. Itera hasta t
def divisores' = Y (\f n m t. (le m t) ((is0 (mod n m)) (false) (f n (suc m) t)) (true))

-- Determina si n tiene algun divisor entre 2 y raiz entera de n
def divisores = \n. divisores' n 2 (sqrt n)

-- Determina si un numero es primo
def esPrimo = \n. (is0 (pred n)) (false) (divisores n)
